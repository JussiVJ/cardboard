plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'java'
    id "com.peterabeles.gversion" version "1.8.3"
}

gversion {
  srcDir = "src/main/java/"
  classPackage = "com.javazilla.bukkitfabric"
  className = "GitVersion"
}
project.compileJava.dependsOn(createVersionFile)

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations { 
    extraLibs
    removeLibs
}

minecraft {
    accessWidener = file("src/main/resources/bukkitfabric.accesswidener")
}

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = 'https://maven.dblsaiko.net/' }
    maven { url = 'https://isaiah.fungus-soft.com/maven-repo' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Spigot API
    extraLibs group: "org.spigotmc", name: "spigot-api", version: "${project.api_version}"
    //extraLibs group: "com.destroystokyo.paper", name: "paper-api", version: "${project.api_version}"
    extraLibs group: "org.jetbrains", name: "annotations", version: "18.0.0"
    extraLibs group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"
    extraLibs "org.yaml:snakeyaml:1.25"

    // NMS Remapping
    extraLibs group: "com.github.IsaiahPatton", name: "SpecialSource", version: "31af248ea6"
    extraLibs group: "com.github.OrionMinecraft", name: "SrgLib", version: "master-SNAPSHOT"

    // Remove parts that are included with Minecraft
    removeLibs group: "com.google.guava", name: "guava", version: "21.0"
    removeLibs group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

    configurations.compile.extendsFrom(configurations.extraLibs)
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

jar {
    from "LICENSE"
    from {
        (configurations.extraLibs - configurations.removeLibs).filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    configurations = [project.configurations.compile]
    dependencies {
      exclude(dependency('com.mojang:minecraft:*'))
      exclude(dependency('org.objectweb:asm:*'))
      exclude(dependency('com.google.guava:guava:*'))
    }
}
